// !Mode:: "ASCII:EDT"
// New Field.edt
// part of the bibMacros package
// (schlicht 20011205)
// $Revision: 1.8 $
/////////////////////////

PushTagsandRegisters;

// Read the global configuration file:
IfStr("%$('BM_conf');","","=",!"Exe('%b\Macros\LaTeX\bibMacros\bibMacros.cfg');");

BeginGroup;
Mark(2);
SetTracking(0);
LetRegNum(6,%l);
Exe("%b\Macros\LaTeX\bibMacros\Get Entry.edt");
SetSel(0);
Goto(2);
IfStr("%$('BM_entry');","","=","Prompt('Outside Bibliography Entry',3,1);JMP('exit');");
IfNum(%!6,"%$('BM_entryEndL');",">","Prompt('Outside Bibliography Entry',3,1);JMP('exit');");
IfNum(%!6,"%$('BM_entryEndL');","=","GotoCL(1,%l-1);");
IfNum(%!6,"%$('BM_entryStartL');","=","CMD('Go To End Of Line');Mark(2);");

// Empty line?
GetLine(1,"%l");
FindInString("%!1","+{~{ |<|>}}",3,2,1);
IfNum(%!2,0,">","",>
 !`CMD("Delete Line");>
   GotoCL(1,%l);>
   CMD("Char Left");>
   Mark(2);`>
);

Release("BMGetField");
Exe("%b\Macros\LaTeX\bibMacros\Get Field.edt");
LetReg(5,"%\");
IfNum("%l","%$('BM_entryStartL');","<",">
    Goto(2);>
    CMD('Char Right');",>
  !"IfStr('%$(|BM_fieldValue|);','','=',>
     !'Goto(2);>
       CMD(|Char Right|);',>
     !'// Last field ending with comma? <>
       GetLength(2,|%$(*BM_fieldValue*);|);>
       ExtractByIndex(2,|%$(*BM_fieldValue*);|,%!2-1,%!2-1);>
       IfStr(|%!2|,|,|,|=|,||,|LetReg(5,*,%\*);|);>
       SetSel(0);'>
    );">
);

SetOK(1);
FindInString("%$('BM_type');","\@\(+$Alpha$\){\{|(}",1,2,11);//)
ReplaceInString("%$('BM_type');","\U0",%!1,%!2,1,9);
Assign("BM_type","%!9");
// Get the admissible items:
IfStr("%$('bib_bibStyle');","","=","",!">
   Assign('BorbName','BM_Fields_%$(|bib_bibStyle|);.cfg');>
   Assign('BorbFile','Macros\LaTeX\bibMacros\Fields_%$(|bib_bibStyle|);.cfg');>
   IfStr('%[|%$(:Borb:);|];%$(|BM_Fields_%$(*bib_bibStyle*);.cfg|);','???','=','Release(''bib_bibStyle'');',>
     'Exe(''%$(|BM_Fields_%$(*bib_bibStyle*);.cfg|);'');');">
);>
Assign("BorbName","BM_Fields.cfg");>
Assign("BorbFile","Macros\LaTeX\bibMacros\Fields.cfg");>
IfStr("%['%$(|Borb|);'];%$('BM_Fields.cfg');","???","=",>
   "Prompt('I did not find the configuration file  ""Fields.cfg"" !',3,1);PopTagsandRegisters;Exit;",>
   "Exe('%$(|BM_Fields.cfg|);');");>
SetOK(1);
FindInString("%$('bib_ENTRIES%$(|bib_bibStyle|);');","{%$('BM_type');}",1,2,0);
IfOK("",!">
   SetOK(1);>
   FindInString('%$(|bib_ENTRIES|);','{%$(|BM_type|);}',1,2,0);>
   IfOK('',!'>
      Assign(''BM_type'',''DEFAULT%$(|bib_bibStyle|);'');');");

IfStr("%$('bib_bibStyle');","","=",>
   "LetReg(1,'');",>
   "LetReg(1,'(%$(|bib_bibStyle|);)');");
MUIReset("New field in %$('BM_type'); %!1");
MUISet('','width','300');
MUISet('','height','350');
MUISet('','position','1');
Assign('mui_margin','8');

// Fields for current item type
MUIAdd("%$('mui_margin');","%$('mui_margin');",280,210, 'BM_new_field','TListBox');
LetRegNum(2,-1);
Call("get_fields");
MUISet('BM_new_field','on double click',!">
    MUIGet('BM_new_field','text',9);>
    Call('evt_insert');");
// Other field
MUIAdd("%$('mui_margin');",228,280,50,'BM_other','TGroupBox','','&Other:');
MUIAdd("%$('mui_margin');","2*%$('mui_margin');",264,0,'field_other','TEdit','BM_other');
MUISet('field_other','on change',!">
    MUISet('btn_ok','enabled','1');>
    MUIGet('field_other','text',9);");
// All, Insert, Cancel
MUIAdd(0,280,320,0,'pnl_modal','TPanel');
  MUIAdd("%$('mui_margin');","%$('mui_margin');",50,0, 'btn_all','TButton','pnl_modal','&All');
    MUISet('btn_all','on click',!'>
       MUISet("BM_new_field","items","","");>
       Assign("BM_type","DEFAULT");>
       LetRegNum(2,-1);>
       Call("get_fields");');
  MUIAdd(125,"%$('mui_margin');",0,0, 'btn_ok','TButton','pnl_modal','&Insert');
     MUISet('btn_ok','default','1');
     MUISet('btn_ok','enabled','0');
     MUISet('btn_ok','on click', !'Call(|evt_insert|);');
  MUIAdd(212,"%$('mui_margin');",0,0, 'btn_cancel','TButton','pnl_modal','Cancel');
     MUISet('btn_cancel','cancel', '1'); // ESCAPE
     MUISet('btn_cancel','on click', !'MUIHide(2);');

MUIShow;

JMP("exit");
:get_fields::
Loop(!`>
  SetOK(1);>
  FindInString("%$('bib_%$(|BM_type|);%$(|bib_bibStyle|);');,%$('BM_other_fields');","\(9+$Alpha$\)\(3@{-}\)\(6@{\<*\>}\),",1,2,1011,%!2+1);>
  IfOK(!">
     IfStr('%#6','','=',!'LetReg(6,||);',!'GetLength(4,|%#6|);ExtractByIndex(6,|%#6|,1,%!4-2);');>
     IfStr('%#3','-','=','',!'>
        IfStr(|%$(*bib_fieldDelim*);|,|quotes|,|=|,!|LetReg(6,*""%!6""*);|,!|LetReg(6,*{%!6}*);|);'>
     );>
     GetTag(9,9);>
     SetOK(1);>
     FindInString('%$(|BM_entry|);','%!9@{ }=',1,3,1);>
     IfOK('',!'>
        SetOK(1);>
        FindInString(''%$(|bib_FIELDS%$(*bib_bibStyle*);|);'',''\{%!9=*\}'',1,3,1);>
        IfOK(!|>
            GetLength(4,||%!9||);>
            ExtractByIndex(8,||%$(*bib_FIELDS%$(:bib_bibStyle:);*);||,%!1+%!4+2,%!3-1);>
            IfStr(||%!8||,||||,||=||,!||>
               SetOK(1);>
               FindInString(*%$(:bib_FIELDS:);*,*\{%!9=**\}*,1,3,1);>
               IfOK(!*ExtractByIndex(8,**%$(:bib_FIELDS:);**,%!1+%!4+2,%!3-1);*,*LetReg(8,**no info**);*);>
            ||);|,>
          !|LetReg(8,*no info*);|);>
        Assign(|BM_new_field_ins_%!9|,|%!6|);>
        Assign(|BM_new_field_%!9|,|%!8|);>
        MUIAddItem(|BM_new_field|,|%!9|);>
        MUISet(|BM_new_field|,|on click|,!|>
            MUISet(''btn_ok'',''enabled'',''1'');>
            MUIGet(''BM_new_field'',''text'',9);>
            MUISet(''BM_new_field'',''hint'',''%$(*BM_new_field_%!9*);'');>
        |);'>
     );",>
    "Stop;">
  );`>
);
Return;

:evt_insert::
// Insert it
IfStr("%!9","","=","",!`>
   MUIGet("BM_new_field","items","1");>
   FindInString("|%!1|","|%!9|",1,2);>
   IfOK("",!"Assign('BM_other_fields','%$(|BM_other_fields|);%!9,');");>
   FindInString("=%!9=","\(0%!9\)",1,2,1,0);>
   ReplaceInString('%!9','\%$(|bib_fieldCase|);0',0,%!2,1,9);>
   Repeat("%$('bib_alignName');",!"LetReg(9,' %!9');");>
   IfStr("%$('bib_alignEqual');","name","=",!"LetReg(9,'%!9 =');");>
   LetRegNum(2,"%$('bib_alignValue');-(%!2+%!1+1+%$('bib_alignName');)");>
   IfNum(%!2,0,">",!"Repeat(%!2,!'LetReg(9,''%!9 '');');");>
   IfStr("%$('bib_alignEqual');","value","=",!"LetReg(9,'%!9= ')");>
   CMD("Char Left");>
   Ins("%!5%!9%!6,");>
   FindInString("%!6","",1,1,0);>
   IfOK("CMD('Previous Bullet');",!">
      CMD('Char Left');>
      FindInString('%!6','""|\}',1,1,1);>
      IfOK('CMD(''Char Left'');');");>
`);
MUIHide(1);
Return;

:exit::
Release("BM_conf");
SetTracking(1);
RestoreFind;
PopTagsandRegisters;
Refresh;
EndGroup;
End;

$Id: bibNewField.edt,v 1.8 2004-04-13 04:23:27+02 schlicht Exp schlicht $
