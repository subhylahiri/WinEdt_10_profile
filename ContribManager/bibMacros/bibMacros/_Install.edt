// -*- ASCII:EDT -*-
// _Install.edt
// installation macro for the bibMacros package
// -----------------------------------------------------

Requires(20120317);

IfIStr("%C","%b\Macros\LaTeX\bibMacros","=","",>
    !"Prompt('You must install the bibMacros package from%\""%b\Macros\LaTeX\bibMacros\"".%\%\Cannot continue!',3,1);Exit;");

PushTagsandRegisters;
Assign("BM_rev","3");
Release("BibTeX_install");
Release("BibTeX_upgrade");

MUIReset('Install bibMacros');
MUISet('','position','1');
MUISet('','width','350');
MUISet('','height','220');
Assign('mui_margin','8');
MUIGet('','client width',9);
LetRegNum(9,"%!9-2*%$('mui_margin');");
Assign('mui_width','%!9');
Assign('mui_button_w','80');
Assign('mui_button_h','25');

MUIAdd(0,0,0,"%$('mui_button_h');+2*%$('mui_margin');","pnl_bottom","TPanel");
   MUISet('pnl_bottom','aligned','alBottom');
   MUIAdd("%$('mui_margin');",0,"%$(`mui_width`);",0,'pnl_modal','TPanel','pnl_bottom');
      MUIAdd(160,"%$('mui_margin');","%$('mui_button_w');","%$('mui_button_h');", 'btn_install','TButton','pnl_modal','&Install');
         MUISet('btn_install','default', '1');
         MUISet('btn_install','on click', 'MUIHide(1);');
      MUIAdd(248,"%$('mui_margin');","%$('mui_button_w');","%$('mui_button_h');", 'btn_cancel','TButton','pnl_modal','&Cancel');
         MUISet('btn_cancel','cancel', '1');
         MUISet('btn_cancel','on click', !'MUIHide(2);');
      MUIAdd(0,"%$('mui_margin');","%$('mui_button_w');+30","%$('mui_button_h');", 'btn_doc','TButton','pnl_modal','Open &Documentation');
         MUISet('btn_doc','on click', !'Run(|%$(*PDF-View*); "%b\Macros\LaTeX\bibMacros\bibMacros.pdf"|);');

MUIAdd("%$('mui_margin');","%$('mui_margin');","%$('mui_width');","70", 'radio_menu','TRadioGroup','','Install as ... ','Main Menu|Popup Menu');
  MUISet('radio_menu','index','0');

MUIAdd("%$('mui_margin');*2","90","%$('mui_width');","","check_cite",'TCheckBox','','Use "Find Entry" for "\cite..." Active Strings');
  MUISet('check_cite','checked','1');

IfFileExists("%b\Macros\LaTeX\bibMacros\bibMacros.cfg",>
   "LetRegNum(0,1);LetRegNum(1,0);LetReg(2,' (old files will be backed up)');",>
   "LetRegNum(0,0);LetRegNum(1,1);LetReg(2,'');");
MUIAdd("%$('mui_margin');*2","120","%$('mui_width');","","check_default",'TCheckBox','','Use default Configuration files%!2');
  MUISet('check_default','enabled','%!0');
  MUISet('check_default','checked','%!1');

MUIShow;

IfOK("","PopTagsandRegisters;Exit;");

JMP("start");
:change_menu::
IfFileExists("%b\ConfigEx\MainMenu.ini","",>
    !"CopyFile('%B\ConfigEx\MainMenu.ini','%b\ConfigEx\MainMenu.ini',1,1);");
CopyFile("%b\Macros\LaTeX\bibMacros\bibMacros.ini","%b\ConfigEx\bibMacros.ini",1,1);
ReadFile("%b\ConfigEx\MainMenu.ini",0);
FindInString("%!0",'\(0<@{ }ITEM="\$Bibtex"@{ }>**\)\(2><@{ }ITEM=\)',8,9,1);
IfOK(!"FindInString('%#0','<\(1@{ }%!5=*>\)',3,4,11);>
   IfOK(!'>
      SubstituteInString(''%#0'',''%#1'',''%!7'',0,1);>
      SubstituteInString(''%\%#0'',''%\'',''%\/%_/ '',0,2);>
      ReplaceInString(''%!0'',''/%_/ @Begin: bibMacros%\/%_/ @Begin: ORIGINAL%!2%\/%_/ @End: ORIGINAL%\%!1%\/%_/ @End: bibMacros%#2'',%!8,%!9,0,0);');");
Return;

:add_menu::
FindInString("%!0","/%_/ @BEGIN: bibMacros",1,2,10); // case-sensitive!
IfOK("",!">
   FindInString('%!0','[END]',1,2);>
   IfOK(!'ReplaceInString(''%!0'',''>
/%_/ @BEGIN: bibMacros%\>
/%_/ @AUTHOR: Robert <w.m.l@gmx.net>%\>
/%_/ @VERSION: 3 (2012/04/11)%\>
%%INCLUDE=""ConfigEx\bibMacros.ini""%\>
/%_/ @END: bibMacros%\%\[END]'',%!1,%!2,0);');");
WriteFile("%b\ConfigEx\%!5.ini","%!0");
LoadConfig("%b\ConfigEx\%!5.ini");
Return;

:do_cite::
LetReg(0,"no");
IfFileExists("%b\Macros\Active Strings\Cite%!1.edt",>
  !"Prompt('The file ""%%b\Macros\Active Strings\Cite%!1.edt""%\already exists. Overwrite it?',1,3,'LetReg(0,''yes'');');",>
  !"LetReg(0,'yes');");
IfStr("%!0","no","<>",>
  !"IfFileExists('%b\Macros\Active Strings\','',!'CreateFolders(''%b\Macros\Active Strings\'',1);');>
    IfFileExists('%b\Macros\Active Strings\Cite%!1.edt',>
      !'CopyFile(''%b\Macros\Active Strings\Cite%!1.edt'',''%b\Macros\Active Strings\Cite%!1.edt.bak'',1,1);');>
    OpenOutput('%b\Macros\Active Strings\Cite%!1.edt');>
       WrS('/%_/ !Mode:: ""ASCII:EDT""%\/%_/ Auto Response to: \cite{');>
       IfStr('%!1','','=',!'WrL(''}'');');>
       IfStr('%!1','-cont','=',!'WrL(''?},,'');');>
       IfStr('%!1','-sel','=',!'WrL(''?,,'');');>
       WrL('/%_/ (bibMacros)%\%\>
Assign(""BM_findEntry_active"",""cite%!1"");%\>
Exe(""%%b\Macros\LaTeX\bibMacros\Find Entry.edt"");%\>
End;');>
    CloseOutput;");
Return;

:do_copy::
IfFileExists('%b\Macros\LaTeX\bibMacros\%!1.%!2',>
  !'Assign(''BibTeX_upgrade'',''upgrade'');>
    CopyFile(''%b\Macros\LaTeX\bibMacros\%!1.%!2'',''%b\Macros\LaTeX\bibMacros\%!1_%$(|BM_prev|);.%!2'',1);');
CopyFile('%b\Macros\LaTeX\bibMacros\%!1_default.%!2','%b\Macros\LaTeX\bibMacros\%!1.%!2',1);
Return;

:start::

MUIGet('radio_menu','index','0');
IfNum(%!0,0,"=",!`>
   LetReg(5,"SHORTCUT");>
   LetReg(6,"shortcut disabled");>
   LetReg(7,"");>
   Call("change_menu");>
   LetReg(5,"MainMenu");>
   Call("add_menu");>
   Assign("BibTeX_install","%\ * Added Main Menu");`,>
 !`LetReg(5,"MACRO");>
   LetReg(6,"popup macro changed");>
   LetReg(7,"    MACRO=""Popup('&BibTeX');""%\");>
   Call("change_menu");>
   WriteFile("%b\ConfigEx\MainMenu.ini","%!0");>
   LoadConfig("%b\ConfigEx\MainMenu.ini");>
   ReadFile("%b\ConfigEx\PopupMenus.ini",0);>
   LetReg(5,"PopupMenus");>
   Call("add_menu");>
   Assign("BibTeX_install","%$('BibTeX_install');%\ * Added Popup Menu");`);

MUIGet('check_cite','checked','0');
IfNum(%!0,1,"=",!`>
   LetReg(1,"");     Call("do_cite");>
   LetReg(1,"-cont");Call("do_cite");>
   LetReg(1,"-sel"); Call("do_cite");>
   Assign("BibTeX_install","%$('BibTeX_install');%\ * Changed Active Strings");`);

MUIGet('check_default','checked','0');
IfNum(%!0,1,"=",!`>
   IfFileExists("%b\Macros\LaTeX\bibMacros\bibMacros.cfg",>
      !"Exe('%b\Macros\LaTeX\bibMacros\bibMacros.cfg');Assign('BM_prev','%$(|BM_rev|);');",>
      !"Assign('BM_prev','prev');");>
   LetReg(2,"cfg");>
     LetReg(1,"bibMacros");      Call("do_copy");>
     LetReg(1,"Fields");         Call("do_copy");>
     LetReg(1,"Fields_apacite"); Call("do_copy");>
     LetReg(1,"Fields_biblatex");Call("do_copy");>
     LetReg(1,"Fields_jurabib"); Call("do_copy");>
   LetReg(2,"bib");>
     LetReg(1,"BMStrings");      Call("do_copy");>
   Assign("BibTeX_install","%$('BibTeX_install');%\ * Copied default Configuration Files");`);

IfFileExists("%b\ConfigEx\UserPreferences.ini",!'LoadConfig("%b\ConfigEx\UserPreferences.ini");');
IfFileExists("%b\ConfigEx\Private.ini",!'LoadConfig("%b\ConfigEx\Private.ini");');

Exe("%b\Macros\LaTeX\bibMacros\bibMacros.cfg");

IfStr("%$('BibTeX_upgrade');","upgrade","=",>
  "Assign('BibTeX_install','%$(|BibTeX_install|);%\>
   (Your old configuration files have been backed up.)');");

PopTagsandRegisters;

IfFileExists("%b\MUI\bibMacros\","","CreateFolders('%b\MUI\bibMacros',1);");
Prompt("The bibMacros package has been successfully installed:%\%$(|BibTeX_install|);%\",0,1);

End;
