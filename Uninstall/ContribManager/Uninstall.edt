// -*- ASCII:EDT -*-

    ClearCheckList;
    AddCheckList('Uninstall package',1,1);
    AddCheckList('Delete local repository',0);
    ShowCheckList('ContribManager package',200,200);
    IfOK(!'Relax;',!'Exit;');
    IfChecked(1,!'Assign("Local","1");',!'Assign("Local","0");');

    Assign("Package","ContribManager");

    PushTagsandRegisters;

    StartWorking('Uninstalling...');
    ProcessMessages;

    ShowConsole(1,1);
    ResetConsole;
    SetConsoleText("Uninstalling...%\%\",1);
    Call('Uninstall_Start');
    Call('Uninstall_Task');
    SetConsoleText("%\Done...%\",1,1);

    PopTagsandRegisters;

    StopWorking;

    Prompt("Uninstallation Complete! See the Console for uninstall info.",0,1);

    Release("Filename");
    Release("Filetype");
    Release("Folder");
    Release("Package");
    Release("Local");

Exit;

:Uninstall_Start:: ==================================================

    // Delete files
    Assign("Folder","ConfigEx");

      Assign("Filetype",".ini");
      Assign("Filename","MainMenu-ContribManager");
      Call('File_Delete');

    Assign("Folder","ContribManager");

      Assign("Filetype",".edt");
      Assign("Filename","CheckAllUpdates");
      Call('File_Delete');
      Assign("Filename","CheckProgramUpdates");
      Call('File_Delete');
      Assign("Filename","CheckUpdates");
      Call('File_Delete');
      Assign("Filename","Cleanup");
      Call('File_Delete');
      Assign("Filename","CleanupUn");
      Call('File_Delete');
      Assign("Filename","DownloadFile");
      Call('File_Delete');
      Assign("Filename","Install_bibMacros");
      Call('File_Delete');
      Assign("Filename","SanitizeMenu");
      Call('File_Delete');
      Assign("Filename","SanitizeSubMenu");
      Call('File_Delete');
      Assign("Filename","ShowInstalled");
      Call('File_Delete');
      Assign("Filename","Uninstall_bibMacros");
      Call('File_Delete');
      Assign("Filename","Website");
      Call('File_Delete');

      Assign("Filetype",".exe");
      Assign("Filename","WinEdt-download");
      Call('File_Delete');
      Assign("Filename","WinEdt-unzip");
      Call('File_Delete');

      Assign("Filetype",".db");
      Assign("Filename","contrib");
      Call('File_Delete');

    // Update config files
    Assign("Folder","ConfigEx");

      Assign("Filetype",".ini");
      Assign("Filename","MainMenu");
      Call('File_Include');

    // Reload personal settings
    IfFileExists("%b\ConfigEx\UserPreferences.ini",!'LoadConfig("%b\ConfigEx\UserPreferences.ini");');
    IfFileExists("%b\ConfigEx\Private.ini",!'LoadConfig("%b\ConfigEx\Private.ini");');

Return;

:File_Delete:: ======================================================

    IfFileExists('%b\%$(|Folder|);\%$(|Filename|);%$(|Filetype|);',!'Relax;',>
      !'SetConsoleText("WARNING: >
File %$(|Filename|);%$(|Filetype|); not found in %$(|Folder|);.%\",1,1);Return;');
    DeleteFile('%b\%$(|Folder|);\%$(|Filename|);%$(|Filetype|);');
    IfOK(!'SetConsoleText("OK:      >
File %$(|Filename|);%$(|Filetype|); deleted from %$(|Folder|);.%\",1,1);',>
         !'SetConsoleText("ERROR:   >
File %$(|Filename|);%$(|Filetype|); not deleted from %$(|Folder|);!!!%\",1,1);');

Return;

:File_Include:: =====================================================

    IfFileExists('%b\%$(|Folder|);\%$(|Filename|);%$(|Filetype|);',!'Relax;',>
      !'SetConsoleText("WARNING: >
File %$(|Folder|);\%$(|Filename|);%$(|Filetype|); not found.%\",1,1);Return;');
    ReadFile("%b\%$(|Folder|);\%$(|Filename|);%$(|Filetype|);",0);
    ReadFile("%C\Data\%$(|Filename|);_add.dat",1);
    FindInString("%!0","%$(|Filename|);-%$(|Package|);.ini",-1,-1);
    IfOK(!'SubstituteInString("%!0","%!1%\","");>
           IfOK(!`SetConsoleText("OK:      >
File %$(|Folder|);\%$(|Filename|);%$(|Filetype|); successfully restored.%\",1,1);`,>
                !`SetConsoleText("ERROR:   >
File %$(|Folder|);\%$(|Filename|);%$(|Filetype|); not restored!!!%\",1,1);`);',>
         !'SetConsoleText("OK:      >
File %$(|Folder|);\%$(|Filename|);%$(|Filetype|); not modified (already OK).%\",1,1);');
    WriteFile("%b\%$(|Folder|);\%$(|Filename|);%$(|Filetype|);","%!0");
    LoadConfig("%b\%$(|Folder|);\%$(|Filename|);%$(|Filetype|);");

Return;

:Uninstall_Task:: ===================================================

    CloseFile("%X");

    // Delete uninstall files
    ListFolderFiles(0,"%b\Uninstall\%$(|Package|);","",1,1);
    LstEnum("%!0","%\");
    LstGetCount(1);
    Repeat(%!1,>
      !`LstGetItem(2,%!c-1);>
        DeleteFile("%b\Uninstall\%$(|Package|);\%!2");>
        ProcessMessages;>
       `);
    LstRelease;

    // Extra Task
    SetFolder("%b");
    Exe('%b\Config\Cleanup.edt');
    RemoveDirectory("%b\ConfigEx");
    RemoveDirectory("%b\Uninstall\%$(|Package|);\Data");
    RemoveDirectory("%b\Uninstall\%$(|Package|);");
    RemoveDirectory("%b\Uninstall");
    // Delete local repository?
    IfStr("%$(|Local|);","0","=",!"Return;");
    ListFolderFiles(1,'%b\ContribManager','',1,1);
    LstEnum('%!1','%\');
    LstGetCount(1);
    Repeat(%!1,>
      !'LstGetItem(2,%!c-1);>
        DeleteFile("%b\ContribManager\%!2");>
        LetReg(3,"%b\ContribManager\%!2");>
        Loop(!`>
          GetFilePath("%!3",3);>
          IfStr("%b","%!3",!">=",!"Stop;",>
            !"RemoveDirectory(|%!3|);>
              IfOK(!|Relax;|,!|Stop;|);");`);>
        ProcessMessages;>
       ');
    LstRelease;

Return;
