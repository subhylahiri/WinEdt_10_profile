// -*- ASCII:EDT -*-
// bibGetEntry.edt
// part of the bibMacros package
// (schlicht 20011204)
// $Revision: 1.8 $
/////////////////////////

PushTagsandRegisters;
// Read the global configuration file:
IfStr("%$('BM_conf');","","=","Exe('%b\Macros\LaTeX\bibMacros\bibMacros.cfg');");

IfStr("%$('BMGetEntry');","bibNewEntry","=",!`>
   BeginGroup;>
   SetTracking(0);>
   LetRegNum(6,%l);>
   LetRegNum(7,%c);>
`);
IfStr("%$('BMGetEntry');","bibCleanEntry","=","","SaveFind;");

SetSearchCurrentDoc;
SetSearchCyclic(0);
SetSearchEntire(0);
SetSearchForward(0);
SetRegEx(1);
SetFindTextSelect(1);
SetSearchCaseSensitive(0);
SetReplaceRespectCaps(0);
SetSearchInline(1);
SetReplacePrompt(0);
SetReplaceNotify(0);
SetNotFoundNotify(0);
SetFindStr("%$('bib_beginLine');\@{~{STRING|PREAMBLE|COMMENT}+$Alpha$@{ }{\{|(}");//)
SearchReset;

// Get the entry:
Mark(0);
IfStr("%$('BMGetEntry');","bibNewEntry","=","","CMD('Char Right');");
IfFound(!">
   IfStr('%$(|BMGetEntry|);','bibCleanEntry','=',!'>
      IfStr(''%$(|bib_typeCase|);'','''',''='','''',!''>
         LetReg(1,|UpperCase|);>
         IfStr(|%$(*bib_typeCase*);|,|L|,|=|,|LetReg(1,||LowerCase||);|);>
         IfStr(|%$(*bib_typeCase*);|,|I|,|=|,|LetReg(1,||InitialCapitals||);|);>
         Do(|%!1|);'');');>
   GetSel(0,1);>
   Assign('BM_type','%!1');>
   IfStr('%$(|BMGetEntry|);','bibDeleteEntry','=',!'>
      CopyToClipboard(''%S'',0);CMD(''Delete'');Ins(''{'');');>
   Assign('BM_entryStartL','%l');>
   Assign('BM_entryStartC','%c');>
   SetOK(1);>
   FindInString('%$(|BM_type|);','+{ }{\{|(}',1,2,11);//)<>
   IfOK(!'>
      ReplaceInString(''%$(|BM_type|);'','''',%!1,%!2-1,0,0);>
      Assign(''BM_type'',''%!0'');>
   ');>
   IfStr('%$(|BMGetEntry|);','bibCleanEntry','=',!'>
      Ins(''%$(|BM_type|);'');>
   ');>
   MatchDelimiter(1,0,0,1);>
   IfOK(!'>
      Assign(''BM_entryStart2L'',''%l'');>
      Assign(''BM_entryStart2C'',''%c'');>
      CMD(''Caret <-> Select'');>
      Assign(''BM_entryEndL'',''%l'');>
      Assign(''BM_entryEndC'',''%c'');>
      GetSel(0,0);>
      Assign(''BM_entry'',''%!0'');',>
     'Release(''BM_entry'');ResetGroup;Stop;');>
   IfStr('%$(|BMGetEntry|);','bibDeleteEntry','=',!'>
      ReplaceInString(''%S'','''',0,0,0,1); CopyToClipboard(''%!1%\'',1);CMD(''Delete'');');>
  ",">
   Goto(0);>
   Release('BM_entry');">
);

IfStr("%$('BMGetEntry');","bibCleanEntry","=","Release('BMGetEntry');End;");

IfStr("%$('BMGetEntry');","bibNewEntry","=",!`>
   Release("BM_conf");>
   Release("BMGetEntry");>
   IfNum(%!6,"%$('BM_entryEndL');",">",>
      "Goto(0);LetReg(0,'');",>
      "GotoCL(1,%l+1);LetReg(0,'%\');">
   );>
   IfStr("%$('BM_entry');","","=","Goto(0);LetReg(0,'');");>
   SetSel(0);>
   IfStr("%$('bib_bibStyle');","","=","",!">
      Assign('BorbName','BM_Fields_%$(|bib_bibStyle|);.cfg');>
      Assign('BorbFile','Macros\LaTeX\bibMacros\Fields_%$(|bib_bibStyle|);.cfg');>
      IfStr('%[|%$(:Borb:);|];%$(|BM_Fields_%$(*bib_bibStyle*);.cfg|);','???','=','',>
        'Exe(''%$(|BM_Fields_%$(*bib_bibStyle*);.cfg|);'');');">
   );>
   Assign("BorbName","BM_Fields.cfg");>
   Assign("BorbFile","Macros\LaTeX\bibMacros\Fields.cfg");>
   IfStr("%['%$(|Borb|);'];%$('BM_Fields.cfg');","???","=",>
      "Prompt('I did not find the configuration file  ""Fields.cfg"" !',3,1);PopTagsandRegisters;Exit;",>
      "Exe('%$(|BM_Fields.cfg|);');");>
   IfStr("%$('bib_%$(|BM_newEntry|);%$(|bib_bibStyle|);');","","=",>
      "IfStr('%$(|bib_%$(*BM_newEntry*);|);','','=',>
         'Prompt(''You did not define the entry type ""%$(|BM_newEntry|);""!'',3,1);PopTagsandRegisters;Exit;',>
         'Release(''bib_bibStyle'');'>
       );">
   );>
   LetReg(6,"");>
   LetReg(7,"");>
   Repeat("%$('bib_alignName');",!"LetReg(6,' %!6');");>
   IfStr("%$('bib_alignKey');","no","=","",!">
      GetLength(2,'%$(|BM_newEntry|);');>
      LetRegNum(2,'%$(|bib_alignValue|);+(%$(|bib_alignKey|);-%!2-1)');>
      IfNum(%!2,0,'>',!'Repeat(%!2,!''LetReg(7,| %!7|);'');');">
   );>
   SetOK(1);>
   FindInString("%$('bib_%$(|BM_newEntry|);%$(|bib_bibStyle|);');","=*,",1,2,11);>
   IfOK(!"ExtractByIndex(3,'%$(|bib_%$(*BM_newEntry*);%$(*bib_bibStyle*);|);',%!1+1,%!2-1);",>
     !"LetReg(3,'%$(|BM_newEntry|);');");>
   SetOK(1);>
   FindInString("%!3","\(0%!3\)",1,2,1);>
   ReplaceInString("@*","\%$('bib_typeCase');0%!7\{,%\",1,2,1,9);>
   LetRegNum(2,0);>
   Loop(!">
      SetOK(1);>
      FindInString('%$(|bib_%$(*BM_newEntry*);%$(*bib_bibStyle*);|);,','\+\(8+$Alpha$\)\(3@{-}\)\(4@{\<*\>}\),',1,2,1001,%!2);>
      IfOK(!'>
         GetTag(3,3);>
         GetTag(4,4);>
         GetTag(8,8);>
         IfStr(''%!4'','''',''='',''LetReg(4,||);'',>
          !''GetLength(5,|%!4|); ExtractByIndex(4,|%!4|,1,%!5-2);''>
         );>
         IfStr(''%!3'',''-'',''='','''',>
          !''IfStr(""%$(|bib_fieldDelim|);"",""quotes"",""="",""LetReg(4,|""""%!4""""|);"",""LetReg(4,|{%!4}|);"");''>
         );>
         LetReg(5,'''');>
         GetLength(1,''%!8'');>
         LetRegNum(1,''%$(|bib_alignValue|);-%$(|bib_alignName|);-%!1-2'');>
         IfNum(%!1,0,''>'',!''Repeat(%!1,!|LetReg(5,|| %!5||);|);'');>
         IfStr(''%$(|bib_alignEqual|);'',''name'',''='',!''>
              LetReg(9,|%!9%!6%!8 =%!5%!4,%\|);'',>
           !''LetReg(9,|%!9%!6%!8%!5= %!4,%\|);''>
         );',>
       !'IfStr(''%$(|bib_lastComma|);'',''no'',''='',>
           !''GetLength(2,|%!9|);>
              SetOK(1);>
              FindInString(|%!9|,|,|,1,2,1110,%!2);>
              ReplaceInString(|%!9|,||,%!1,%!2,0,9);''>
         );>
         LetReg(9,''%!9}%\'');>
         Stop;');>
   ");>
   CMD("Go To Beginning Of Line");>
   Mark(0);>
   InsText("%!0%!9");>
   Goto(0);>
   CMD("Next Bullet");>
   SetTracking(1);>
   Refresh;>
   EndGroup;>
`);

RestoreFind;
Release("BMGetEntry");
PopTagsandRegisters;
End;

$Id: bibGetEntry.edt,v 1.8 2006-11-08 02:44:21+01 schlicht Exp schlicht $
