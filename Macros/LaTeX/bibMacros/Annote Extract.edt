// !Mode:: "ASCII:EDT"
// Annote Extract.edt
// extract `annote'-fields
// and write them into separate files
// part of the bibMacros package
// (schlicht 20010626)
// $Revision: 1.9 $
/////////////////////////////

PushTagsandRegisters;

//Assign("bib_annotePath", ""); // path for annotation files, relative to %p

// Read the global configuration file, if it exists
IfFileExists("%b\Macros\LaTeX\bibMacros\bibMacros.cfg",>
   "Exe('%b\Macros\LaTeX\bibMacros\bibMacros.cfg');",>
   "IfFileExists('%B\Macros\LaTeX\bibMacros\bibMacros.cfg',>
      'Exe(''%B\Macros\LaTeX\bibMacros\bibMacros.cfg'');',>
      'Assign(''bib_beginLine'',''<'');'>
    );">
);

// Get the bibliography:
// We are in a BibTeX file:
Assign("BM_stop","0");
IfisMode("BibTeX","%!m",>
  !"SetSel(0);>
    GetSel(1,0);>
    Assign('BM_file','%p\%n%t');",>
// else get bibliography (from aux file, to be on the safe side)
  !`Assign("BM_file","%P\%N");>
    IfFileExists("%$('BM_file');.aux",>
     !"ReadFile('%$(|BM_file|);.aux',0);",>
     !"Prompt('I need the file%\   ""%$(|BM_file|);.aux"".%\%\Run LaTeX on your main file, and then we can try again.',2,1); PopTagsandRegisters;Exit;");>
    FindInString("%!0","\\bibdata\{*\}",1,2,11);>
    IfOK(!"ExtractByIndex(1,'%!0',%!1+9,%!2-1);",>
      "Prompt('I found no \bibliography command.',3,1); PopTagsandRegisters;Exit;");>
    LetReg(0,"");>
    LetRegNum(3,-1);>
    LetRegNum(4,-1);>
    Loop(!:>
       FindInString("%!1","{~,}*,",2,3,1011,%!3+1);>
       IfOK(>
         !"ExtractByIndex(6,'%!1',%!2,%!3-1);",>
         !"ExtractByIndex(6,'%!1',%!4+1,%!3+1);>
           Assign('BM_stop','1');>
           Stop;">
       );>
       IfFileExists("%b\Macros\LaTeX\bibMacros\Get File.edt",">
          Exe('%b\Macros\LaTeX\bibMacros\Get File.edt');",">
            IfFileExists('%B\Macros\LaTeX\bibMacros\Get File.edt',>
               'Exe(''%B\Macros\LaTeX\bibMacros\Get File.edt'');',>
               'Assign(''BM_getFile'',''noBM'');');");>
       IfStr("%$('BM_getFile');","cancel","=","PopTagsandRegisters;Exit;");>
       IfStr("%$('BM_getFile');","noBM","=",>
        !"IfStr('%$(|BM_%$(*BM_file*);_%!6|);','','=',!'Assign(''BM_%$(|BM_file|);_%!6'',''%P'');');>
          IfFileExists('%$(|BM_%$(*BM_file*);_%!6|);\%!6.bib',>
            !'ReadFile(''%$(|BM_%$(*BM_file*);_%!6|);\%!6.bib'',4);',>
            !'Prompt(''I don''''t know where the file%\   ""%!6.bib"" is!%\%\Can you help me, please?%\(If you were using the complete bibMacros package%\I would probably be able to find the file.)'',2,3,>
              !''Browse(4,|Looking for ""%!6.bib""||*.bib|,|%P|);>
                 GetFilePath(|%!4|,2);>
                 Assign(|BM_%$(*BM_file*);_%!6|,|%!2|);>
                 ReadFile(|%!4|,4);'',>
               ''PopTagsandRegisters;Exit;''>
             );'>
          );">
       );>
       LetReg(0,'%!0%!4%\');>
       LetRegNum(4,%!3);>
       IfNum('%$(|BM_stop|);',1,'=','Stop;');>
   :);>
`);


LetRegNum(9,0);
Assign("BM_annoteFiles",">
 @COMMENT{%\>
 The following annotation files have been created:%\>
 *************************************************");
Release("BM_annoteFilesErrors");

StartWorking("extract annote...");
GetLength(3,"%!0");>
Loop(!`>
   Release("working");>
   Repeat("76-((%!9*75)/%!3)",!"Assign('working','%$(|working|);|');");>
   StartWorking("%$('working');");>
   SetOK(1);>
   FindInString("%!0","<@{ }annot{e|ation}@{ }=@{ }\{",7,9,1001,%!9);>
   IfOK(!">
// max. 4 nested levels should suffice:
      FindInString('%!0','\{@{>|{\{@{>|{\{@{>|{\{@{>|{\{*\}}|^{\}}}*\}}|^{\}}}*\}}|^{\}}}*\}}|^{\}}}*\}@{,}',8,9,1011,%!9-1);>
// if not take 5:
//      FindInString('%!0','\{@{>|{\{@{>|{\{@{>|{\{@{>|{\{@{>|{\{*\}}|^{\}}}*\}}|^{\}}}*\}}|^{\}}}*\}}|^{\}}}*\}}|^{\}}}*\}@{,}',8,9,1011,%!9-1);>
      ExtractByIndex(1,'%!0',%!9,%!9);>
      IfStr('%!1','}','=',>
         !'ExtractByIndex(1,''%!0'',%!8+1,%!9-1);',>
         !'ExtractByIndex(1,''%!0'',%!8+1,%!9-2);');>
      GetLength(5,'%!1');>
      SetOK(1);>
      FindInString('%!0','%$(|bib_beginLine|);\@+$Alpha$@{ }{\{|(}*,',6,8,1111,%!9);//)<>
      IfOK(!'>
         ExtractByIndex(2,''%!0'',%!6+1,%!8);>
         FindInString(''%!2'',''{\{|(}*,'',6,8,11);//)<>
         ExtractByIndex(2,''%!2'',%!6+1,%!8-1);>
         FormatString(''%!2'',2,''trimboth'');>
         SetOK(1);>
         FindInString(''%!2'',''\(\\|/|:|\?|""|\>|\<|\|\)'',6,8,11);>
         IfOK(!''>
             GetTag(0,6);>
             Assign(|BM_annoteFilesErrors|,>
               |%$(||BM_annoteFilesErrors||);%\>
  * The entry key ""%!2"" contains the character ""%!6"",%\>
    which is not allowed in filenames.%\>
    I couldn''''t write the corresponding annotation file.%\>
    Please rename the entry and run the macro again.%\|);'',>
          !''IfFileExists(|%p\%$(*bib_annotePath*);\%!2.tex|,>
               !|Prompt(||File ""%!2.tex"" already exists.%\Overwrite it?||,1,4,>
                  !||OpenOutput(""%p\%$(*bib_annotePath*);\%!2.tex"");>
                     WrL(""%!1"");>
                     CloseOutput;>
                     Assign(""BM_annoteFiles"",""%$(*BM_annoteFiles*);%\   \input{%!2.tex}"");>
                     ReplaceInString(""%!0"","""",%!7,%!9,0,0);>
                     LetRegNum(9,%!9-%!5);||,>
                   ||||,>
                   ||PopTagsandRegisters;Exit;||);|,>
                !|OpenOutput(""%p\%$(*bib_annotePath*);\%!2.tex"");>
                  WrL(""%!1"");>
                  CloseOutput;>
                  Assign(""BM_annoteFiles"",""%$(*BM_annoteFiles*);%\   \input{%!2.tex}"");>
                  ReplaceInString(""%!0"","""",%!7,%!9,0,0);>
                  LetRegNum(9,%!9-%!5);|);>
           '');',>
        'Prompt(''Error with %!1'');');>
     ","Stop;");>
`);

LetReg(0,"%!0%\%\");
GetFileType("%$('BM_file');",1);
IfStr("%!1",".bib","=",>
   !"Assign('BM_file','%p\%n');");
OpenOutput("%$('BM_file');-extracted.bib");
WrL("%!0%\%\");
WrL("%$('BM_annoteFiles');");
WrL("%$('BM_annoteFilesErrors');%\ }");
CloseOutput;

StopWorking;
PopTagsandRegisters;
Open("%$('BM_file');-extracted.bib");
End;


//===============================================================================
 This macro and the macro Annote Extract.edt deal with the "annote" or
 "annotation" field of bibliography entries, which is used by some BibTeX styles.
 These styles usually allow you to put the annotations either directly in the
 bibliography database or in separate files with the same name as the respective
 bibliography items.

  * Annote Extract.edt
    extracts all "annote" or "annotation" fields from a bibliography file, writing
    them into separate files;
  * Annote Merge.edt
    searches for annotation files with the same name as the bibliography entries,
    merging everything into a new file called "<file name>-merged.bib".

 This macro supposes that the "annote"/"annotation" fields are delimited by {braces}

 The path from which annotation files should be merged can be configured at the top
 of this file or in bibMacros.cfg, or at the end of the bibliography.

 Robert <w.m.l@gmx.net>
//===============================================================================
